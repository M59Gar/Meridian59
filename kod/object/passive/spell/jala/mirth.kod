% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Mirth is Jala

constants:

   include blakston.khd

resources:

   Mirth_name_rsc = "mirth"
   Mirth_icon_rsc = imirth.bgf
   Mirth_desc_rsc = \
      "This song makes people happy!  It is a lively tune, which makes people "
      "feel as if they have a new start on life.  The song causes some people "
      "to smile, often resulting in better moods.  A good mood allows others "
      "to fight evil more effectively.  "
      "It requires 1 sapphire to cast."

   mirth_enter_rsc = "The nearby shanty brings an undeniable cheer to the area."
   mirth_leave_rsc = "The mirthful mood passes."

   Mirth_song = main.mid
 
classvars:

   vrName = Mirth_name_rsc
   vrIcon = Mirth_icon_rsc
   vrDesc = Mirth_desc_rsc

   jala_song_enter = mirth_enter_rsc
   jala_song_leave = mirth_leave_rsc
   
   viSpell_num = SID_MIRTH
   viSchool = SS_JALA
   viSpell_level = 1

   viMana = 2
   viManaDrain = 2
   viDrainTime = 5000

   viSpellExertion = 2      
   viChance_To_Increase = 25

   viHarmful = FALSE
   viBaseRange = 5

properties:

   prMusic = Mirth_song
      
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Sapphire,1],plReagents);

      return;
   }

   EnterSong(who=$,iVolume=0)
   {
      if random(1,100) < iVolume
      {
         Send(who,@SetAction,#action=UA_HAPPY);
         Send(who,@AddAttackModifier,#what=self);       
      }
      else
      {
         % No frowning during the happy song.
         if Send(who,@GetAction) = UA_SAD
         {
            Send(who,@SetAction,#action=UA_NORMAL);
         }
      }
      
      propagate;
   }
   
   LeaveSong(who=$,iVolume=0)
   {
      Send(who,@RemoveAttackModifier,#what=self);
      
      propagate;
   }

   % Stuff we handle to be an attack modifier.

   ModifyHitRoll(who = $,what = $,hit_roll = $)
   {
      % Only add hit against evil targets.
      if Send(what,@GetKarma,#detect=TRUE) < 0
      {
         return hit_roll + 100;
      }

      return hit_roll;
   }
   
   ModifyDamage(who = $,what = $,damage = $)
   {
      % Only add damage against evil targets.
      if Send(what,@GetKarma,#detect=TRUE) < 0
      {
         % Roughly equal to Killing Fields
         return damage + Random(1,5);
      }

      return damage;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
